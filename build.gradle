plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id 'org.beryx.jlink' version '2.24.1'
    id 'org.jetbrains.kotlin.jvm' version '1.6.10'
}

group 'com.hiberus.anaya'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

sourceCompatibility = '17'
targetCompatibility = '17'

mainClassName = "$moduleName/com.hiberus.anaya.redmineeditor.Main"

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'com.hiberus.anaya.redmineeditor'
    mainClass = 'com.hiberus.anaya.redmineeditor.Main'
}

javafx {
    version = '17'
    modules = ['javafx.controls', 'javafx.web', 'javafx.fxml']
}

//noinspection SpellCheckingInspection
dependencies {
    implementation 'org.json:json:20211205'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.6.10'
    // implementation 'com.taskadapter:redmine-java-api:4.0.0.rc4' // useless api, cant search using multiple filters
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages',
               '--add-modules', 'jdk.crypto.ec,jdk.localedata' // crypto: fixes handshake error, localedata: allows non-english locales (for evidences)
    ]
    launcher {
        name = 'RedmineEditor'
    }
    mergedModule {
        additive = true
        forceMerge "kotlin" // https://issueexplorer.com/issue/beryx/badass-jlink-plugin/165
    }
    targetPlatform("linux") {/* Default settings */ } // https://badass-jlink-plugin.beryx.org/releases/latest/
    targetPlatform("windows") {
        //noinspection GroovyAssignabilityCheck
        jdkHome = "${buildDir}/../../win-sdk/jdk-17.0.2+8"
        addExtraModulePath("${buildDir}/../../win-sdk/javafx-jmods-17.0.2")
    }
    targetPlatform("personal") {/* Default settings */ }
    launcher {
        noConsole = true
    }
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "17"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "17"
    }
}

task copyConf {
    doLast {
        // copy things, hardcoded paths
        project.logger.lifecycle "Copying Configuration files"

        // configurations
        copy {
            from "${buildDir}/../conf"
            into "$buildDir/image/RedmineEditor-linux/conf"
        }
        copy {
            from "${buildDir}/../conf"
            into "$buildDir/image/RedmineEditor-windows/conf"
        }
        copy {
            from "${buildDir}/../conf"
            from("${buildDir}/../conf_personal") { duplicatesStrategy = DuplicatesStrategy.INCLUDE }
            into "$buildDir/image/RedmineEditor-personal/conf"
        }

        // launchers
        copy {
            from "${buildDir}/../launcher/RedmineEditor"
            into "$buildDir/image/RedmineEditor-linux"
        }
        copy {
            from "${buildDir}/../launcher/RedmineEditor.bat"
            into "$buildDir/image/RedmineEditor-windows"
        }
        copy {
            from "${buildDir}/../launcher/RedmineEditor"
            into "$buildDir/image/RedmineEditor-personal"
        }

        // copy changelog
        copy {
            from "$sourceSets.main.output.resourcesDir/com/hiberus/anaya/redmineeditor/changelog.txt"
            into buildDir
            rename "changelog.txt", "Readme.md"
        }

    }
}

task cleanImage(type: Delete) {
    // delete invalid launchers
    delete files(
            "${buildDir}/image/RedmineEditor-windows/bin/RedmineEditor",
            "${buildDir}/image/RedmineEditor-linux/bin/RedmineEditor.bat"
    )
}

// https://handstandsam.com/2021/06/07/run-custom-gradle-task-after-build/
tasks.named("jlink") { finalizedBy("copyConf", "cleanImage") }
tasks.named("jlinkZip") { dependsOn("copyConf", "cleanImage") }
