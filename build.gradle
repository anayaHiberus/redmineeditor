//file:noinspection GroovyAssignabilityCheck
//file:noinspection GroovyUnusedAssignment
//file:noinspection GrUnresolvedAccess
plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id 'org.beryx.jlink' version '2.24.1'
    id 'org.jetbrains.kotlin.jvm' version '1.6.0-RC'
}

group 'com.hiberus.anaya'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.7.2'
}

sourceCompatibility = '16'
targetCompatibility = '16'

mainClassName = "$moduleName/com.hiberus.anaya.redmineeditor.Main"

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'com.hiberus.anaya.redmineeditor'
    mainClass = 'com.hiberus.anaya.redmineeditor.Main'
}

javafx {
    version = '16'
    modules = ['javafx.controls', 'javafx.web', 'javafx.fxml']
}

//noinspection SpellCheckingInspection
dependencies {
    implementation "org.json:json:20210307"
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.6.0-RC'
    // implementation 'com.taskadapter:redmine-java-api:4.0.0.rc4' // useless api, cant search using multiple filters
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'RedmineEditor'
    }
    mergedModule {
        additive = true
        forceMerge "kotlin" // https://issueexplorer.com/issue/beryx/badass-jlink-plugin/165
    }
    targetPlatform("personal") {/* Default settings */ } // https://badass-jlink-plugin.beryx.org/releases/latest/
    targetPlatform("linux") {/* Default settings */ }
    targetPlatform("windows") {
        jdkHome = "${buildDir}/../../win-sdk/jdk-16.0.2.7"
        addExtraModulePath("${buildDir}/../../win-sdk/javafx-jmods-16")
    }
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "16"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "16"
    }
}

task copyConf(type: Copy) {
    // copy configuration folder, hardcoded things
    println("Copying Configuration files")

    into("${buildDir}/image")
    include("**/*.*")

    into("RedmineEditor-personal/conf") {
        from("${buildDir}/../conf")
    }
    into("RedmineEditor-linux/conf") {
        from("${buildDir}/../conf")
        exclude("**/settings.properties")
    }
    into("RedmineEditor-windows/conf") {
        from("${buildDir}/../conf")
        exclude("**/settings.properties")
    }
}

// https://handstandsam.com/2021/06/07/run-custom-gradle-task-after-build/
tasks.named("jlink") { finalizedBy("copyConf") }
tasks.named("jlinkZip") { dependsOn("copyConf") }
